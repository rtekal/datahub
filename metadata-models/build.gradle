import io.datahubproject.GenerateJsonSchemaTask

apply plugin: 'java-library'
apply plugin: 'pegasus'

tasks.withType(JavaCompile).configureEach {
  javaCompiler = javaToolchains.compilerFor {
    languageVersion = JavaLanguageVersion.of(8)
  }
}
tasks.withType(Test).configureEach {
  javaLauncher = javaToolchains.launcherFor {
    languageVersion = JavaLanguageVersion.of(8)
  }
}

dependencies {
  api spec.product.pegasus.data
    constraints {
      implementation('org.apache.commons:commons-text:1.10.0') {
        because 'Vulnerability Issue'
      }
    }
  api project(':li-utils')
  dataModel project(':li-utils')

  testImplementation externalDependency.guava
}

mainAvroSchemaJar.dependsOn generateAvroSchema

pegasus.main.generationModes = [PegasusGenerationMode.PEGASUS, PegasusGenerationMode.AVRO]


tasks.register('generateJsonSchema', GenerateJsonSchemaTask) {
  it.setInputDirectory("$projectDir/src/mainGeneratedAvroSchema")
  it.setOutputDirectory("$projectDir/src/generatedJsonSchema")
  dependsOn generateAvroSchema
}

clean {
  project.delete("$projectDir/src/generatedJsonSchema")
}